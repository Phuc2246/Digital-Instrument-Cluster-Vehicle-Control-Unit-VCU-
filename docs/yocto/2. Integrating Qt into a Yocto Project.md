# Integrating Qt into a Yocto Project (Raspberry Pi 4 – EGLFS)

## 1. Prerequisites on Host Machine

Before working with Yocto and Qt, install the required Qt development packages on your host machine (for local testing and building tools):

```bash
sudo apt update
sudo apt install qtcreator qtbase5-dev qtbase5-dev-tools qtdeclarative5-dev \
qml-module-qtquick2 qml-module-qtquick-controls2 qt5-qmake cmake \
build-essential libgl1-mesa-dev
````

These packages allow you to:

* Edit and test Qt applications locally.
* Use `qmake` and Qt Creator.

---

## 2. Adding `meta-qt5` to the Yocto Project

### 2.1 Clone the meta-qt5 Layer

Inside your Yocto `sources` directory, clone the **meta-qt5** layer:

```bash
git clone -b kirkstone https://github.com/meta-qt5/meta-qt5.git
```

### 2.2 Add the Layer to `bblayers.conf`

```bash
bitbake-layers add-layer ../meta-qt5
```

---

## 3. Configuring `local.conf`

Open `conf/local.conf` and make the following changes:

```bash
# Target machine
MACHINE = "raspberrypi4"
INIT_MANAGER = "systemd"

# Enable EGLFS and OpenGL in distro features
DISTRO_FEATURES:append = " eglfs opengl pam"

# Install essential networking and Qt packages in the target image
IMAGE_INSTALL:append = " wpa-supplicant networkmanager \
    qtbase qtdeclarative qtquickcontrols2 qtgraphicaleffects"

# Include the custom Qt application (recipe name below)
IMAGE_INSTALL:append = " project1"
```

---

## 4. Building the Image with Qt Support

Run:

```bash
bitbake core-image-base
```

---

## 5. Adding a Custom Qt Application to Yocto

### 5.1 Create a Recipe Directory

```bash
mkdir -p meta-myproj/recipes-apps/project1/files
```

Copy all your Qt application source files into `files/`.

---

### 5.2 Create the Recipe File `project1.bb`

```bitbake
SUMMARY = "Qt Quick demo application - Project1"
DESCRIPTION = "A Qt Quick application with custom QML gauges"
LICENSE = "CLOSED"

DEPENDS += "qtbase qtdeclarative qtquickcontrols2 qtgraphicaleffects"

inherit qmake5 systemd

SRC_URI = " \
    file://main.cpp \
    file://main.qml \
    file://qml.qrc \
    file://project1.pro \
    file://Dashboard1.qml \
    file://Dashboard2.qml \
    file://Gauge.qml \
    file://MapGauge.qml \
    file://SideGauge.qml \
    file://WelcomeScreen.qml \
    file://radialbar.cpp \
    file://radialbar.h \
    file://canhandler.cpp \
    file://canhandler.h \
    file://io_configs/io_config.json \
    file://img \
    file://assets \
    file://project1.service \
"

S = "${WORKDIR}"
B = "${WORKDIR}/build"

do_install() {
    # --- Binary: /usr/bin/project1 ---
    install -d ${D}${bindir}
    install -m 0755 ${B}/project1 ${D}${bindir}/project1

    # --- io_config.json: /usr/bin/io_config.json ---
    # (service dùng WorkingDirectory=/usr/bin, nên loadIOConfig("io_config.json") sẽ đúng)
    install -d ${D}${bindir}/io_configs
    install -m 0644 ${WORKDIR}/io_configs/io_config.json \
          ${D}${bindir}/io_configs/io_config.json

    # --- QML & assets (nếu app cần file ngoài resource) ---
    install -d ${D}${datadir}/project1
    cp -r ${S}/*.qml ${D}${datadir}/project1/ || true
    cp -r ${S}/img    ${D}${datadir}/project1/ || true
    cp -r ${S}/assets ${D}${datadir}/project1/ || true
}

FILES:${PN} += " \
    ${bindir}/project1 \
    ${bindir}/io_configs \
    ${datadir}/project1 \
"

SYSTEMD_AUTO_ENABLE:${PN} = "enable"

```

---

### 5.3 Create the Systemd Service File

File: `meta-myproj/recipes-apps/project1/files/project1.service`

```ini
[Unit]
Description=Qt Application - project1
After=systemd-user-sessions.service

[Service]
ExecStart=/usr/bin/env QT_QPA_PLATFORM=eglfs /usr/bin/project1
Restart=always
User=root
WorkingDirectory=/usr/bin

[Install]
WantedBy=multi-user.target
```

---

## 6. Building and Deploying the Application

1. Add your recipe to the image by ensuring it’s listed in `local.conf`:

```bash
IMAGE_INSTALL:append = " project1"
```

2. Build the image:

```bash
bitbake core-image-base
```

3. Flash the generated image to your target board (SD card).

---

## 7. Running the Qt Application on EGLFS

When your system boots, you can run manually:

```bash
cd /usr/bin/
export QT_QPA_PLATFORM=eglfs
./project1
```

or check the systemd auto-launch status:

```bash
systemctl status project1
journalctl -u project1 -e
```

---

**Result:**
Your Yocto image for Raspberry Pi 4 boots directly into your Qt dashboard via **EGLFS**, without needing Weston or Wayland.



